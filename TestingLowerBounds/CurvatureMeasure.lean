/-
Copyright (c) 2024 Lorenzo Luccioli. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne, Lorenzo Luccioli
-/
import TestingLowerBounds.ForMathlib.ByParts
import TestingLowerBounds.ForMathlib.LeftRightDeriv
import Mathlib.MeasureTheory.Integral.FundThmCalculus
import Mathlib.Probability.Notation

open MeasureTheory Set StieltjesFunction ProbabilityTheory
open scoped Classical

namespace ConvexOn

variable {ùí≥ : Type*} {mùí≥ : MeasurableSpace ùí≥} {Œº ŒΩ : Measure ùí≥} {f : ‚Ñù ‚Üí ‚Ñù} {Œ≤ Œ≥ x t : ‚Ñù}

-- Should we define this to be some junk value if f is not convex?
-- This way we could avoid having to state the convexity every time.
-- This may be put in some other place, maybe directly in the stieltjes file.

/-- The curvature measure induced by a convex function. It is defined as the only measure that has
the right derivative of the function as a CDF. -/
noncomputable
irreducible_def curvatureMeasure (f : ‚Ñù ‚Üí ‚Ñù) : Measure ‚Ñù :=
  if hf : ConvexOn ‚Ñù univ f then hf.rightDerivStieltjes.measure else 0

lemma curvatureMeasure_of_convexOn (hf : ConvexOn ‚Ñù univ f) :
    curvatureMeasure f = hf.rightDerivStieltjes.measure := by
  rw [curvatureMeasure, dif_pos hf]

lemma curvatureMeasure_of_not_convexOn (hf : ¬¨ConvexOn ‚Ñù univ f) :
    curvatureMeasure f = 0 := by
  rw [curvatureMeasure, dif_neg hf]

instance {f : ‚Ñù ‚Üí ‚Ñù} : IsLocallyFiniteMeasure (curvatureMeasure f) := by
  simp_rw [curvatureMeasure]
  split_ifs <;> infer_instance

/-- A Taylor formula for convex functions in terms of the right derivative
and the curvature measure. -/
theorem convex_taylor (hf : ConvexOn ‚Ñù univ f) (hf_cont : Continuous f) {a b : ‚Ñù} :
    f b - f a - (rightDeriv f a) * (b - a)  = ‚à´ x in a..b, b - x ‚àÇ(curvatureMeasure f) := by
  have h_int : IntervalIntegrable (rightDeriv f) ‚Ñô a b := hf.rightDeriv_mono.intervalIntegrable
  rw [‚Üê intervalIntegral.integral_eq_sub_of_hasDeriv_right hf_cont.continuousOn
    (fun x _ ‚Ü¶ hf.hadDerivWithinAt_rightDeriv x) h_int]
  simp_rw [‚Üê neg_sub _ b, intervalIntegral.integral_neg, curvatureMeasure_of_convexOn hf,
    mul_neg, sub_neg_eq_add, mul_comm _ (a - b)]
  let g := StieltjesFunction.id + StieltjesFunction.const (-b)
  have hg : g = fun x ‚Ü¶ x - b := rfl
  rw [‚Üê hg, integral_stieltjes_meas_by_parts g hf.rightDerivStieltjes]
  simp only [Real.volume_eq_stieltjes_id, add_apply, id_apply, id_eq, const_apply, add_right_neg,
    zero_mul, zero_sub, measure_add, measure_const, add_zero, neg_sub, sub_neg_eq_add, g]
  rfl

end ConvexOn
